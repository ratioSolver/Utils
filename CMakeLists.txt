cmake_minimum_required(VERSION 3.21)
project(utils VERSION 0.2.0 LANGUAGES CXX)

list(APPEND CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake/Modules")
include(Sanitizers)

include(CTest)
enable_testing()

set(INT_TYPES "int" "long" "long long")
set(INT_TYPE "long" CACHE STRING "Integer type")
set_property(CACHE INT_TYPE PROPERTY STRINGS ${INT_TYPES})
list(FIND INT_TYPES ${INT_TYPE} INT_TYPE_INDEX)
if(${INT_TYPE_INDEX} EQUAL -1)
    message(FATAL_ERROR "Invalid integer type: ${INT_TYPE}")
endif()

set(LOGGING_LEVELS "TRACE" "DEBUG" "INFO" "WARN" "ERROR" "FATAL")
set(LOGGING_LEVEL "INFO" CACHE STRING "Logging level")
set_property(CACHE LOGGING_LEVEL PROPERTY STRINGS ${LOGGING_LEVELS})
list(FIND LOGGING_LEVELS ${LOGGING_LEVEL} LOGGING_LEVEL_INDEX)
if(${LOGGING_LEVEL_INDEX} EQUAL -1)
    message(FATAL_ERROR "Invalid logging level: ${LOGGING_LEVEL}")
endif()

option(BUILD_LISTENERS "Build listners" OFF)
option(UTILS_A_STAR_ENABLE_NAVIGATION "Enable navigation hooks for the A* solver" OFF)
option(BUILD_CRYPTO "Build crypto" OFF)

if(LOGGING_LEVEL STREQUAL "TRACE")
    set(LOG_LEVEL 6)
elseif(LOGGING_LEVEL STREQUAL "DEBUG")
    set(LOG_LEVEL 5)
elseif(LOGGING_LEVEL STREQUAL "INFO")
    set(LOG_LEVEL 4)
elseif(LOGGING_LEVEL STREQUAL "WARN")
    set(LOG_LEVEL 3)
elseif(LOGGING_LEVEL STREQUAL "ERROR")
    set(LOG_LEVEL 2)
elseif(LOGGING_LEVEL STREQUAL "FATAL")
    set(LOG_LEVEL 1)
endif()

message(STATUS "Integer type: ${INT_TYPE}")
message(STATUS "Logging level: ${LOGGING_LEVEL}")

add_library(utils src/integer.cpp src/rational.cpp src/inf_rational.cpp src/lin.cpp src/tableau.cpp src/timer.cpp)
target_compile_features(utils PUBLIC cxx_std_17)
target_include_directories(utils PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>)
target_compile_definitions(utils PUBLIC INT_TYPE=${INT_TYPE} LOGGING_LEVEL=${LOG_LEVEL})
setup_sanitizers(utils)

message(STATUS "Build listners: ${BUILD_LISTENERS}")
if(BUILD_LISTENERS)
    target_compile_definitions(utils PUBLIC BUILD_LISTENERS)
endif()

message(STATUS "A* navigation hooks: ${UTILS_A_STAR_ENABLE_NAVIGATION}")
if(UTILS_A_STAR_ENABLE_NAVIGATION)
    target_compile_definitions(utils PUBLIC UTILS_A_STAR_ENABLE_NAVIGATION)
endif()

message(STATUS "Build crypto: ${BUILD_CRYPTO}")
if(BUILD_CRYPTO)
    find_package(OpenSSL REQUIRED)

    target_sources(utils PRIVATE src/crypto.cpp)
    target_link_libraries(utils PUBLIC OpenSSL::Crypto)
endif()

if(BUILD_TESTING)
    add_subdirectory(tests)
endif()

set(CPACK_PROJECT_NAME utils)
set(CPACK_PROJECT_VERSION ${PROJECT_VERSION})
include(CPack)
